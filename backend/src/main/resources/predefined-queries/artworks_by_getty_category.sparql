PREFIX arp:  <http://arp.ro/schema#>
PREFIX aat:  <http://vocab.getty.edu/aat/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prov: <http://www.w3.org/ns/prov#>

SELECT DISTINCT ?artworkUri ?title ?category_ro ?category_en ?aatCode
WHERE {
  ?artwork a arp:Artwork ;
           arp:title ?title ;
           arp:category ?category .

  ?mapping skos:exactMatch ?aatCode ;
           rdfs:label ?labelRo .
  FILTER(LANG(?labelRo) = "ro")

  OPTIONAL {
    ?mapping rdfs:label ?labelEn .
    FILTER(LANG(?labelEn) = "en")
  }

  FILTER(?aatCode = aat:300122621)  # pastel

  FILTER(
    CONTAINS(LCASE(STR(?category)), LCASE(STR(?labelRo))) ||
    (BOUND(?labelEn) && CONTAINS(LCASE(STR(?category)), LCASE(STR(?labelEn))))
  )

  BIND(?artwork AS ?artworkUri)
  BIND(STR(?labelRo) AS ?category_ro)
  BIND(IF(BOUND(?labelEn), STR(?labelEn), STR(?labelRo)) AS ?category_en)
}
ORDER BY ?artwork

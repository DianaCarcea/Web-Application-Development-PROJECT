PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>

SELECT ?artwork
       ?title
       ?image
       ?inception
       ?startTime
       ?pointInTime
       ?publicationDate
       ?description
       ?artist
       ?artistName
       ?heightValue
       ?heightUnitLabel
       ?widthValue
       ?widthUnitLabel
       ?conditionLabel
       ?location ?locationName
       (GROUP_CONCAT(DISTINCT ?instanceLabel; separator="|") AS ?instanceLabels)
       (GROUP_CONCAT(DISTINCT ?subclassLabel; separator="|") AS ?subclassLabels)
       (GROUP_CONCAT(DISTINCT ?artFormLabel; separator="|") AS ?artFormLabels)
       (GROUP_CONCAT(DISTINCT ?genreLabel; separator="|") AS ?genreLabels)
       (GROUP_CONCAT(DISTINCT ?materialLabel; separator="|") AS ?materialLabels)
       (GROUP_CONCAT(DISTINCT ?inventoryNumber; separator=", ") AS ?inventoryNumbers)
WHERE {
  VALUES ?artwork { wd:Q_ID_PLACEHOLDER }

  OPTIONAL { ?artwork rdfs:label ?title FILTER(LANG(?title)="en") }
  OPTIONAL { ?artwork wdt:P18 ?image }
  OPTIONAL { ?artwork wdt:P571 ?inception }
  OPTIONAL { ?artwork wdt:P580 ?startTime }
  OPTIONAL { ?artwork wdt:P585 ?pointInTime }
  OPTIONAL { ?artwork wdt:P577 ?publicationDate }
  OPTIONAL { ?artwork schema:description ?description FILTER(LANG(?description)="en") }

  OPTIONAL {
      ?artwork p:P2048 ?heightStmt .
      ?heightStmt ps:P2048 ?heightValue .
      ?heightStmt psv:P2048 ?heightValueNode .
      ?heightValueNode wikibase:quantityUnit ?heightUnit .
      ?heightUnit rdfs:label ?heightUnitLabel FILTER(LANG(?heightUnitLabel)="en")
  }

  OPTIONAL {
      ?artwork p:P2049 ?widthStmt .
      ?widthStmt ps:P2049 ?widthValue .
      ?widthStmt psv:P2049 ?widthValueNode .
      ?widthValueNode wikibase:quantityUnit ?widthUnit .
      ?widthUnit rdfs:label ?widthUnitLabel FILTER(LANG(?widthUnitLabel)="en")
  }

  OPTIONAL {
      ?artwork wdt:P5816 ?condition .
      ?condition rdfs:label ?conditionLabel FILTER(LANG(?conditionLabel)="en")
  }

  # Instance and subclass
  OPTIONAL {
      ?artwork wdt:P31 ?instance .
      ?instance rdfs:label ?instanceLabel FILTER(LANG(?instanceLabel)="en") .
      OPTIONAL {
          ?instance wdt:P279 ?subclass .
          ?subclass rdfs:label ?subclassLabel FILTER(LANG(?subclassLabel)="en")
      }
  }

  # Art form
  OPTIONAL {
      ?artwork wdt:P2596 ?artForm .
      ?artForm rdfs:label ?artFormLabel FILTER(LANG(?artFormLabel)="en")
  }

  # Genre
  OPTIONAL {
      ?artwork wdt:P136 ?genre .
      ?genre rdfs:label ?genreLabel FILTER(LANG(?genreLabel)="en")
  }
  OPTIONAL {
      ?artwork wdt:P186 ?material .
      ?material rdfs:label ?materialLabel FILTER(LANG(?materialLabel)="en")
  }
  OPTIONAL {
        ?artwork p:P217 ?inventoryStmt .
        ?inventoryStmt ps:P217 ?inventoryNumber .
  }

  OPTIONAL {
      ?artwork wdt:P276 ?location .
      ?location rdfs:label ?locationName FILTER(LANG(?locationName)="en")
  }

  # Artist
  OPTIONAL {
      ?artwork wdt:P170 ?artist .
      ?artist rdfs:label ?artistName FILTER(LANG(?artistName)="en")
  }
}
GROUP BY ?artwork ?title ?image ?inception ?startTime ?pointInTime ?publicationDate ?description ?artist ?artistName ?heightValue ?heightUnitLabel ?widthValue ?widthUnitLabel ?conditionLabel ?location ?locationName

PREFIX arp: <http://arp.ro/schema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dct: <http://purl.org/dc/terms/>

SELECT ?subject
       (SAMPLE(?title_raw) AS ?title)
       (SAMPLE(?img_raw) AS ?img)
       (SAMPLE(?category_raw) AS ?category)
       (SAMPLE(?condition_raw) AS ?condition)
       (SAMPLE(?inv_raw) AS ?inv)
       (SAMPLE(?desc_raw) AS ?desc)
       (SAMPLE(?cimec_raw) AS ?cimec)
       (SAMPLE(?wikidataLink_val) AS ?wikidataLink)
       (SAMPLE(?license_raw) AS ?license)
       (SAMPLE(?date_raw) AS ?date)
       (SAMPLE(?dimensions_raw) AS ?dimensions)
       (SAMPLE(?registrarUri_raw) AS ?registrarUri)
       (SAMPLE(?registrarName_raw) AS ?registrarName)
       (SAMPLE(?recordedAt_raw) AS ?recordedAt)
       (SAMPLE(?validatorUri_raw) AS ?validatorUri)
       (SAMPLE(?validatorName_raw) AS ?validatorName)
       (SAMPLE(?validatedAt_raw) AS ?validatedAt)
       (SAMPLE(?artistName_raw) AS ?artistName)
       (SAMPLE(?artistUri_raw) AS ?artistUri)
       (SAMPLE(?museumName_raw) AS ?museumName)
       (SAMPLE(?museumUri_raw) AS ?museumUri)
       (GROUP_CONCAT(DISTINCT ?cls; separator="; ") AS ?classifications)
       (GROUP_CONCAT(DISTINCT ?tech; separator="; ") AS ?techniques)
       (GROUP_CONCAT(DISTINCT ?culture; separator="; ") AS ?cultures)
       (GROUP_CONCAT(DISTINCT ?mat; separator="; ") AS ?materialsUsed)

       (GROUP_CONCAT(
         DISTINCT CONCAT(
           COALESCE(?ownerName, STR(?ownerUri)),
           "||",
           COALESCE(STR(?ownStart), ""),
           "||",
           COALESCE(STR(?ownEnd), "")
         );
         separator=";;"
      ) AS ?ownershipHistory)

WHERE {
  ?subject a arp:Artwork .

  # Folosim _raw pentru variabilele interne pentru a evita conflictele cu AS ?variabila
  OPTIONAL { ?subject arp:title ?title_raw }
  OPTIONAL { ?subject arp:imageLink ?img_raw }
  OPTIONAL { ?subject arp:dimensions ?dimensions_raw }
  OPTIONAL { ?subject arp:category ?category_raw }
  OPTIONAL { ?subject arp:condition ?condition_raw }
  OPTIONAL { ?subject arp:inventoryNumber ?inv_raw }
  OPTIONAL { ?subject arp:description ?desc_raw }
  OPTIONAL { ?subject arp:cimecLink ?cimec_raw }
  OPTIONAL { ?subject arp:wikidataLink ?wikidataLink_val }
  OPTIONAL { ?subject dct:license ?license_raw }

  OPTIONAL { ?subject arp:classification ?cls }
  OPTIONAL { ?subject arp:culture ?culture }

  OPTIONAL {
    ?subject arp:recordedAt ?recordedAt_raw .
    ?subject arp:recordedBy ?registrarUri_raw .
    ?registrarUri_raw arp:name ?registrarName_raw .
  }

  OPTIONAL {
    ?subject arp:validatedAt ?validatedAt_raw .
    ?subject arp:validatedBy ?validatorUri_raw .
    ?validatorUri_raw arp:name ?validatorName_raw .
  }

  OPTIONAL {
    ?subject prov:wasGeneratedBy ?activity .
    OPTIONAL { ?activity arp:startedAtTime ?date_raw }
    OPTIONAL { ?activity arp:technique ?tech }
    OPTIONAL { ?activity arp:materialsUsed ?mat }
  }

  OPTIONAL {
    ?subject prov:wasAttributedTo ?artistUri_raw .
    ?artistUri_raw arp:name ?artistName_raw
  }

  OPTIONAL {
    ?subject arp:currentLocation ?museumUri_raw .
    ?museumUri_raw arp:name ?museumName_raw
  }

  OPTIONAL {
        ?subject arp:hasOwnership ?ownership .
        ?ownership a arp:TransferOfCustody .

        OPTIONAL { ?ownership arp:startedAtTime ?ownStart }
        OPTIONAL { ?ownership arp:endedAtTime ?ownEnd }
        OPTIONAL {
          ?ownership prov:wasAssociatedWith ?ownerUri .
          OPTIONAL { ?ownerUri arp:name ?ownerName }
        }
      }
}
GROUP BY ?subject
LIMIT 30
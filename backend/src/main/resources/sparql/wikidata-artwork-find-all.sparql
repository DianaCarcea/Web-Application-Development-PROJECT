PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>

SELECT ?title ?artwork
 (SAMPLE(?image) AS ?imageSample)
 (SAMPLE(?inception) AS ?inceptionSample)
 (SAMPLE(?startTime) AS ?startTimeSample)
 (SAMPLE(?pointInTime) AS ?pointInTimeSample)
 (SAMPLE(?publicationDate) AS ?publicationDateSample)
 (SAMPLE(?description) AS ?descriptionSample)
 (SAMPLE(?artistName) AS ?artistNameSample)
 (SAMPLE(?artist) AS ?artistUriSample)
 (SAMPLE(?heightValue) AS ?heightValueSample)
 (SAMPLE(?heightUnitLabel) AS ?heightUnitSample)
 (SAMPLE(?widthValue) AS ?widthValueSample)
 (SAMPLE(?widthUnitLabel) AS ?widthUnitSample)
 (SAMPLE(?conditionLabel) AS ?conditionSample)
 (SAMPLE(?instanceLabel) AS ?instanceSample)
 (SAMPLE(?subclassLabel) AS ?subclassSample)
 (SAMPLE(?artFormLabel) AS ?artFormSample)
 (SAMPLE(?genreLabel) AS ?genreSample)

WHERE {
  ?artwork wdt:P31 wd:Q838948 .  # artwork
  ?artwork rdfs:label ?title FILTER(LANG(?title)="en")

  OPTIONAL { ?artwork wdt:P18 ?image }
  OPTIONAL { ?artwork wdt:P571 ?inception }
  OPTIONAL { ?artwork wdt:P580 ?startTime }
  OPTIONAL { ?artwork wdt:P585 ?pointInTime }
  OPTIONAL { ?artwork wdt:P577 ?publicationDate }
  OPTIONAL { ?artwork schema:description ?description FILTER(LANG(?description)="en") }
  OPTIONAL {
      ?artwork p:P2048 ?heightStatement .
      ?heightStatement ps:P2048 ?heightValue .
      ?heightStatement psv:P2048 ?heightValueNode .
      ?heightValueNode wikibase:quantityUnit ?heightUnit .
      ?heightUnit rdfs:label ?heightUnitLabel FILTER(LANG(?heightUnitLabel)="en")
  }

  # WIDTH
  OPTIONAL {
      ?artwork p:P2049 ?widthStatement .
      ?widthStatement ps:P2049 ?widthValue .
      ?widthStatement psv:P2049 ?widthValueNode .
      ?widthValueNode wikibase:quantityUnit ?widthUnit .
      ?widthUnit rdfs:label ?widthUnitLabel FILTER(LANG(?widthUnitLabel)="en")
  }

  OPTIONAL {
      ?artwork wdt:P5816 ?condition .
      ?condition rdfs:label ?conditionLabel FILTER(LANG(?conditionLabel)="en")
  }



  OPTIONAL {
      ?artwork wdt:P31 ?instance .
      ?instance rdfs:label ?instanceLabel FILTER(LANG(?instanceLabel)="en")
  }
  OPTIONAL {
      ?instance wdt:P279 ?subclass .
      ?subclass rdfs:label ?subclassLabel FILTER(LANG(?subclassLabel)="en")
  }
  OPTIONAL {
      ?artwork wdt:P2596 ?artForm .
      ?artForm rdfs:label ?artFormLabel FILTER(LANG(?artFormLabel)="en")
  }
  OPTIONAL {
      ?artwork wdt:P136 ?genre .
      ?genre rdfs:label ?genreLabel FILTER(LANG(?genreLabel)="en")
  }


   OPTIONAL {
        ?artwork wdt:P170 ?artist .
        ?artist rdfs:label ?artistName FILTER(LANG(?artistName)="en")
   }
}
GROUP BY ?title ?artwork
LIMIT 5
